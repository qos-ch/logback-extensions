buildscript {
  repositories {
    google()
    jcenter()
  }
  dependencies {
//    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    classpath 'org.ajoberstar:grgit:2.1.0'

    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}
plugins {
  id 'io.codearte.nexus-staging' version '0.11.0' // must be in root project
  id 'net.researchgate.release' version '2.6.0'
}
apply plugin: 'org.ajoberstar.grgit'
apply from: 'gradle/readme.gradle'

allprojects  {
  apply plugin: 'maven'

  group = 'org.logback-extensions'
  version = VERSION_NAME
}

subprojects {
  apply plugin: 'java'
  apply from: "${rootProject.rootDir}/gradle/deploy.gradle"

  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    // Warn about deprecations
    //options.compilerArgs << '-Xlint:deprecation'
    // Warn about unchecked usages
    options.compilerArgs << '-Xlint:unchecked'
    // Don't warn about using source/target 1.5 option
    options.compilerArgs << '-Xlint:-options'

    options.debug(['debugLevel': 'source,lines,vars'])
    options.debug = VERSION_NAME.contains('SNAPSHOT')
  }

  repositories {
    mavenLocal()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
  }

  dependencies {
    testCompile 'org.mockito:mockito-core:1.9.0'
    testCompile 'junit:junit:4.12'
  }

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  javadoc {
    failOnError false
    // disable javadoc lint warnings (e.g., missing javadoc)
    options.addBooleanOption('Xdoclint:none', true)
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  artifacts {
    archives sourcesJar
    archives javadocJar
  }
}

release {
  tagTemplate = 'v_${version}'
  preTagCommitMessage = ':cloud: Release'
  tagCommitMessage = ':cloud: Release'
  newVersionCommitMessage = ':cloud: Bump'

//  versionPropertyFile = '../gradle.properties'
  versionProperties = ['VERSION_NAME']

  git {
    requireBranch = ''
  }
}

nexusStaging {
  packageGroup = 'org.logback-extensions'
}
